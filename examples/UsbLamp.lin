display "\nLoading UsbLamp.lin"

class UsbLamp ()
   var exePath "C:\\workspace\\DevEnvironment\\scripts\\usblamp\\USBDOSAP.EXE"
   var operations
      tag Alist
         quote
            ("allflashoff"    = "10 20 7 0")
               "alldark"      = "10 12 0 7"
               "allon"        = "10 12 7 0"
               "blueflash"    = "10 20 0 4"
               "blueflashoff" = "10 20 4 0"
               "blueoff"      = "10 12 0 4"
               "blueon"       = "10 12 4 0"
               "greenflash"   = "10 20 0 1"
               "greenflashoff"= "10 20 1 0"
               "greenoff"     = "10 12 0 1"
               "greenon"      = "10 12 1 0"
               "redflash"     = "10 20 0 2"
               "redflashoff"  = "10 20 2 0"
               "redoff"       = "10 12 0 2"
               "redon"        = "10 12 2 0"
               "alloff"       = "meta"
        
   def call-lamp(codes)
      System
         apply !exec
            cons exePath (codes(!split))

   def !change((op = String))
       var codes (operations (!lookup op))
       cond
          (equal? op "alloff")
             !change "allflashoff"
             !change "alldark"
          (null? codes) 
             raise "UsbLamp sent bad op-code."  
          else
             call-lamp codes
      
class BuildNotification()
    define mediaStore "c:\\WINNT\\Media\\"
    
    def play(soundfile-name)
        Sound(!play (mediaStore (!+ soundfile-name)))
        the soundfile-name
    
    def beep()
        display "\a \a"
        
    def !alertUsers(reason)
        cond
          (equal? reason "build-start")
              play "notify.wav"
              UsbLamp (!change "alloff")
              UsbLamp (!change "blueflash")
          (equal? reason "build-ok")
              UsbLamp (!change "redoff")
              UsbLamp (!change "redflashoff")
              UsbLamp (!change "greenon")
          (equal? reason "build-failed")
              play "Ringin.wav"
              UsbLamp (!change "alloff")
              UsbLamp (!change "redflash")
              play "Ringin.wav"
              play "Ringin.wav"
          (equal? reason "build-complete")
              beep
              UsbLamp (!change "blueflashoff")
              UsbLamp (!change "greenon")
          (equal? reason "deploy-start")
              beep
              UsbLamp (!change "greenoff")
              UsbLamp (!change "greenflash")
          (equal? reason "deploy-complete")
              play "tada.wav"
              UsbLamp (!change "greenflashoff")
              UsbLamp (!change "greenon")
               
               
df httpd-serve (request)
   ; print request
   var params (request(!getParameters))
   var operation (params (!lookup "op"))
   BuildNotification(!alertUsers operation)
   var result
    list 200 "text/html"
      template
          html()
             head()
             body()
                form()
                   input((name="op") (value= ,operation))
                   input((type="submit"))
   stdout(!format "~a~%" operation)
   result  
                      

   