display "\nLoading UsbLamp.lin"

class UsbLamp ()
   var exePath "C:\\workspace\\DevEnvironment\\scripts\\usblamp\\USBDOSAP.EXE"
   var operations
      tag Alist
         quote
            ("allflashoff"    : "10 20 7 0")
               "alldark"      : "10 12 0 7"
               "allon"        : "10 12 7 0"
               "blueflash"    : "10 20 0 4"
               "blueflashoff" : "10 20 4 0"
               "blueoff"      : "10 12 0 4"
               "blueon"       : "10 12 4 0"
               "greenflash"   : "10 20 0 1"
               "greenflashoff": "10 20 1 0"
               "greenoff"     : "10 12 0 1"
               "greenon"      : "10 12 1 0"
               "redflash"     : "10 20 0 2"
               "redflashoff"  : "10 20 2 0"
               "redoff"       : "10 12 0 2"
               "redon"        : "10 12 2 0"
               "alloff"       : "meta"
        
   def call-lamp(codes)
      System
         apply _exec
            cons exePath (codes(_split))

   def _change((op : String))
       var codes (operations (_lookup op))
       cond
          (equal? op "alloff")
             _change "allflashoff"
             _change "alldark"
          (null? codes) 
             raise "UsbLamp sent bad op-code."  
          else
             call-lamp codes
      
class BuildNotification()
    define mediaStore "c:\\WINNT\\Media\\"
    
    def play(soundfile-name)
        Sound(_play (mediaStore (_+ soundfile-name)))
        the soundfile-name
    
    def beep()
        display "\a \a"
        
    def _alertUsers(reason)
        cond
          (equal? reason "build-start")
              play "notify.wav"
              UsbLamp (_change "alloff")
              UsbLamp (_change "blueflash")
          (equal? reason "build-ok")
              UsbLamp (_change "redoff")
              UsbLamp (_change "redflashoff")
              UsbLamp (_change "greenon")
          (equal? reason "build-failed")
              play "Ringin.wav"
              UsbLamp (_change "alloff")
              UsbLamp (_change "redflash")
              play "Ringin.wav"
              play "Ringin.wav"
          (equal? reason "build-complete")
              beep
              UsbLamp (_change "blueflashoff")
              UsbLamp (_change "greenon")
          (equal? reason "deploy-start")
              beep
              UsbLamp (_change "greenoff")
              UsbLamp (_change "greenflash")
          (equal? reason "deploy-complete")
              play "tada.wav"
              UsbLamp (_change "greenflashoff")
              UsbLamp (_change "greenon")
               
               
df httpd-serve (request)
   ; print request
   var params (request(_getParameters))
   var operation (params (_lookup "op"))
   BuildNotification(_alertUsers operation)
   var result
    list 200 "text/html"
      template
          html()
             head()
             body()
                form()
                   input((name:"op") (value: ,operation))
                   input((type:"submit"))
   stdout(_format "~a~%" operation)
   result  
                      

   